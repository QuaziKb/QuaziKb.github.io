<html>
<head>
	<link rel="stylesheet" type="text/css" href="index.css">
	<META name="keywords" content="Quazi, TowerClimb, Tower Climb, game, gamedev, indie, engineer, engineering, programming, Kevin"> 
	<title>Quazi (Homepage)</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
	
</head>
<script src="../js/QZMath.js"></script>
<body bgcolor="#e8e8e8" text="#000000">

<div id="wrap">

<h1>WebGL Canvas</h1>
<canvas id="c"></canvas>

</div>

</body>
  
<!-- you can and should delete this script. it is only used on the site to help with errors -->
<script id="2d-vertex-shader" type="notjs">

  // an attribute will receive data from a buffer
  attribute vec4 a_position;
  varying vec4 p_position;
  // all shaders have a main function
  void main() {

    // gl_Position is a special variable a vertex shader
    // is responsible for setting
    gl_Position = a_position;
	p_position = gl_Position;
  }

</script>
<script id="2d-fragment-shader" type="notjs">

// fragment shaders don't have a default precision so we need
// to pick one. mediump is a good default
precision mediump float;
uniform vec4 u_color;
uniform mat3 camera_mat3;
uniform vec3 ray_origin;
uniform float t;
varying vec4 p_position;

vec2 opS( vec2 d1, vec2 d2 )
{
	return (d1.x>d2.x) ? d1 : d2;
}

vec2 opU( vec2 d1, vec2 d2 )
{
	return (d1.x<d2.x) ? d1 : d2;
}

vec3 opRep( vec3 p, vec3 c )
{
    return mod(p,c)-0.5*c;
}

float sdPlane( vec3 p )
{
	return p.y;
}

float sdSphere( vec3 p, float s )
{
	return length(p)-s;
}

float sdBox( vec3 p, vec3 b )
{
    vec3 d = abs(p) - b;
    return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
}

//------------------------------------------------------------------



vec3 opTwist( vec3 p )
{
    float  c = cos(10.0*p.y+10.0);
    float  s = sin(10.0*p.y+10.0);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

float interp(float a, float b, float t)
{
	return a+(a-b)*t;
}

//------------------------------------------------------------------

vec2 map( in vec3 pos )
{
   vec2 res = vec2( mix(max(sdSphere(    pos-vec3( sin(t),cos(t)*0.1, 0.0), 0.25 ),-sdSphere(    pos-vec3( sin(t+0.25),cos(t*2.0+0.25)*0.1+0.25, 0.0), 0.25 )),sdBox(pos-vec3(0.0,0.0,0.0),vec3(1.25,0.25,0.25)),sin(t*0.337+0.3)*0.5+0.5), 0.0 );
	//vec2 res = vec2(mix(sdSphere(    pos-vec3( sin(t),cos(t)*0.1, 0.0), 1.0 ),sdSphere(    pos-vec3( sin(t*1.15+0.25),cos(t*2.0+0.25)*0.1+0.25, 0.0), 1.0 ),0.75)+0.5,0.0);
	
    return res;
}

float calcSoftshadow( in vec3 ro, in vec3 rd, in float mint, in float tmax )
{
	float res = 1.0;
    float t = mint;
    for( int i=0; i<16; i++ )
    {
		float h = map( ro + rd*t ).x;
        res = min( res, 8.0*h/t );
        t += clamp( h, 0.02, 0.10 );
        if( h<0.001 || t>tmax ) break;
    }
    return clamp( res, 0.0, 1.0 );
}

vec3 calcNormal( in vec3 pos )
{
    vec2 e = vec2(1.0,-1.0)*0.5773*0.0005;
    return normalize( e.xyy*map( pos + e.xyy ).x + 
					  e.yyx*map( pos + e.yyx ).x + 
					  e.yxy*map( pos + e.yxy ).x + 
					  e.xxx*map( pos + e.xxx ).x );
    /*
	vec3 eps = vec3( 0.0005, 0.0, 0.0 );
	vec3 nor = vec3(
	    map(pos+eps.xyy).x - map(pos-eps.xyy).x,
	    map(pos+eps.yxy).x - map(pos-eps.yxy).x,
	    map(pos+eps.yyx).x - map(pos-eps.yyx).x );
	return normalize(nor);
	*/
}

float calcAO( in vec3 pos, in vec3 nor )
{
	float occ = 0.0;
    float sca = 1.0;
    for( int i=0; i<5; i++ )
    {
        float hr = 0.01 + 0.12*float(i)/4.0;
        vec3 aopos =  nor * hr + pos;
        float dd = map( aopos ).x;
        occ += -(dd-hr)*sca;
        sca *= 0.95;
    }
    return clamp( 1.0 - 3.0*occ, 0.0, 1.0 );    
}

vec2 castRay( in vec3 ro, in vec3 rd )
{
    float tmin = 0.0;
    float tmax = 50.0;
   
   
    float t = tmin;
    float m = -1.0;
    for( int i=0; i<100; i++ )
    {
	    float precis = 0.0005*t;
	    vec2 res = map( ro+rd*t );
        if( res.x<precis || t>tmax ) break;
        t += res.x;
	    m = res.y;
    }

    if( t>tmax ) m=-1.0;
    return vec2( t, m );
}

vec3 render( in vec3 ro, in vec3 rd )
{ 
    vec3 col = vec3(0.7, 0.9, 1.0) +rd.y*0.8;
    vec2 res = castRay(ro,rd);
    float t = res.x;
	float m = res.y;
    if( m>-0.5 )
    {
        vec3 pos = ro + t*rd;
        vec3 nor = calcNormal( pos );
        vec3 ref = reflect( rd, nor );
        
        // material        
		col = vec3(0.5,0.5,0.5);

        // lighitng        
        float occ = calcAO( pos, nor );
		vec3  lig = normalize( vec3(-0.4, 0.7, -0.6) );
        vec3  hal = normalize( lig-rd );
		float amb = clamp( 0.5+0.5*nor.y, 0.0, 1.0 );
        float dif = clamp( dot( nor, lig ), 0.0, 1.0 );
        float bac = clamp( dot( nor, normalize(vec3(-lig.x,0.0,-lig.z))), 0.0, 1.0 )*clamp( 1.0-pos.y,0.0,1.0);
        float dom = smoothstep( -0.1, 0.1, ref.y );
        float fre = pow( clamp(1.0+dot(nor,rd),0.0,1.0), 2.0 );
        
        dif *= calcSoftshadow( pos, lig, 0.02, 2.5 );
        dom *= calcSoftshadow( pos, ref, 0.02, 2.5 );

		float spe = pow( clamp( dot( nor, hal ), 0.0, 1.0 ),16.0)*
                    dif *
                    (0.04 + 0.96*pow( clamp(1.0+dot(hal,rd),0.0,1.0), 5.0 ));

		vec3 lin = vec3(0.0);
        lin += 1.30*dif*vec3(1.00,0.80,0.55);
        lin += 0.40*amb*vec3(0.40,0.60,1.00)*occ;
        lin += 0.50*dom*vec3(0.40,0.60,1.00)*occ;
        lin += 0.50*bac*vec3(0.25,0.25,0.25)*occ;
        lin += 0.25*fre*vec3(1.00,1.00,1.00)*occ;
		col = col*lin;
		col += 10.00*spe*vec3(1.00,0.90,0.70);

    	col = mix( col, vec3(0.8,0.9,1.0), 1.0-exp( -0.0002*t*t*t ) );
    }

	return vec3( clamp(col,0.0,1.0) );
}

//------------------------------------------------------------------

void main() {
	// gl_FragColor is a special variable a fragment shader
	// is responsible for setting
	
	vec3 ray_direction = camera_mat3*normalize(vec3(p_position.x,p_position.y-1.0,2.0));	
	//gl_FragColor = u_color*p_position*test; // return redish-purple
	vec3 color = render(ray_origin,ray_direction);
	
	gl_FragColor = vec4(color,1.0);
}

</script>
<script src="js/webgl-utils.js"></script>
<script src="js/main.js"></script>